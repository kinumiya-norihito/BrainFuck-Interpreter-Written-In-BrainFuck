{計算用スペース(とりあえず4byte)}
>>>>
{全体ループフラグの設定}
-
>
{テープのメモリ領域を確保}
{サイズにして2×256}
{先頭4byteは入出力用}
>>>>-[[->>+<<]>>-]
{コードの保存領域}
{先頭4byteは入出力用}
>>>>
{0xFF以外の文字を読み続ける}
,+[->+>,+]-
{コードの保存領域の先頭にもどる}
<[[-]<<]<<<
{フラグ付近まで戻る}
-[[-<<+>>]<<-]<<
{テープの参照位置を1に設定}
+
<<
{改行を表示する}
++++++++++.[-]
<
{ループフラグがFFの間常に実行}
[
	{コードに移動}
	>>>>-[[->>+<<]>>-]>>>
	{コード読み込みのポインタを1ずらす}
	+
	{コード読み込みポインタから読み込み位置への数値を作る}
	[->+<<+>]<[->+<]>>
	{数値を元に目的値まで移動}
	{戻りアドレスの仕様が少し変わった}
	[[->>+<<]+>>-]<
	{返り値を書き込み}
	[->+>>+<<<]>>>[-<<<+>>>]<<
	{綱があったら戻れる}
	<<[[-]>>[-<<+>>]<<<<]
	{フラグ付近まで戻る}
	-[[-<<+>>]>>[-<<+>>]<<<<-]>>[-<<<<<<<<<+>>>>>>>>>]<<<<<<<<<
	{文字の条件分岐(ここから)}
	-------------------------------------------
	[->>+>+<<<]>>[-<<+>>]+>[[-]<[-]>]<
	[
		{数値をインクリメント(ここから)}
		{テープ読み込みポインタに移動}
		[-]>>
		{テープ読み込みポインタから読み込み位置への数値を作る}
		[->+<<+>]<[->+<]>>
		{数値を元に目的値まで移動}
		[[->>+<<]+>>-]<
		{インクリメント}
		+
		{綱があったら戻れる}
		<[[-]<<]<
		{数値をインクリメント(ここまで)}
	]<<-
	[->>+>+<<<]>>[-<<+>>]+>[[-]<[-]>]<
	[
		[-]>>
		[->+<<+>]<[->+<]>>
		[[->>+<<]+>>-]<
		,
		<[[-]<<]<
	]<<-
	[->>+>+<<<]>>[-<<+>>]+>[[-]<[-]>]<
	[
		[-]>>
		[->+<<+>]<[->+<]>>
		[[->>+<<]+>>-]<
		-
		<[[-]<<]<
	]<<-
	[->>+>+<<<]>>[-<<+>>]+>[[-]<[-]>]<
	[
		[-]>>
		[->+<<+>]<[->+<]>>
		[[->>+<<]+>>-]<
		.
		<[[-]<<]<
	]<<--------------
	[->>+>+<<<]>>[-<<+>>]+>[[-]<[-]>]<
	[
		[-]>>
		-
		<<
	]<<--
	[->>+>+<<<]>>[-<<+>>]+>[[-]<[-]>]<
	[
		[-]>>
		+
		<<
	]<<-----------------------------
	[->>+>+<<<]>>[-<<+>>]+>[[-]<[-]>]<
	[
		
		[-]>>
		[->+<<+>]<[->+<]>>
		[[->>+<<]+>>-]<
		[->+>>+<<<]>>>[-<<<+>>>]<<
		<<[[-]>>[-<<+>>]<<<<]<+>>>[[-]<<<[-]>>>]<<<
		{取ってきた値が0なら実行}
		[
			[-]
			{相対levelを1増やす}
			+
			{相対levelが0以外なら実行}
			[
				>>>-[[->>+<<]>>-]>>>+
				[->+<<+>]<[->+<]>>
				[[->>+<<]+>>-]<
				[->+>>+<<<]>>>[-<<<+>>>]<<
				<<[[-]>>[-<<+>>]<<<<]
				-[[-<<+>>]>>[-<<+>>]<<<<-]>>[-<<<<<<<<<+>>>>>>>>>]<<<<<<<<<
				-------------------------------------------------------------------------------------------
				[->>>+>>+<<<<<]>>>[-<<<+>>>]+>>[[-]<<[-]>>]<<
				[
					[-]
					<+>
				]<<<--
				[->>>+>>+<<<<<]>>>[-<<<+>>>]+>>[[-]<<[-]>>]<<
				[
					[-]
					<->
				]<<<
				[-]
				>>
			]
		]
	]<<--
	[->>+>+<<<]>>[-<<+>>]+>[[-]<[-]>]<
	[
		[-]
		{相対levelを位置減らす}
		-
		{相対levelが0以外なら実行}
		[
			>>>-[[->>+<<]>>-]>>>-
			[->+<<+>]<[->+<]>>
			[[->>+<<]+>>-]<
			[->+>>+<<<]>>>[-<<<+>>>]<<
			<<[[-]>>[-<<+>>]<<<<]
			-[[-<<+>>]>>[-<<+>>]<<<<-]>>[-<<<<<<<<<+>>>>>>>>>]<<<<<<<<<
			-------------------------------------------------------------------------------------------
			[->>>+>>+<<<<<]>>>[-<<<+>>>]+>>[[-]<<[-]>>]<<
			[
				[-]
				<+>
			]<<<--
			[->>>+>>+<<<<<]>>>[-<<<+>>>]+>>[[-]<<[-]>>]<<
			[
				[-]
				<->
			]<<<
			[-]
			>>
		]
		>>>-[[->>+<<]>>-]>>>-
		<-[[-<<+>>]<<-]<<<<<
	]<<------------------------------------------------------------------------------------------------------------------------------------------------------------------
	[->>+>+<<<]>>[-<<+>>]+>[[-]<[-]>]<
	[
		{終了条件}
		[-]
		<[-]>
	]<<--
	{絶対停止線}
	[-]
	>
]
